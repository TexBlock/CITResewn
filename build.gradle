plugins.apply 'dev.architectury.loom'
//plugins.apply 'io.shcm.shsupercm.fabric.fletchingtable'

version = "${property('mod.version')}+${stonecutter.current.version}"
group = 'org.thinkingstudio.citfoxified'

var modPlatform = property("loom.platform")
def isJava21 = stonecutter.eval(stonecutter.current.version, ">=1.20.5")

repositories {
    maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url "https://maven.neoforged.net/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"

    if (modPlatform == "forge") {
        mappings "net.fabricmc:yarn:${property('deps.yarn')}:v2"
    } else if (modPlatform == "neoforge") {
        mappings loom.layered {
            mappings "net.fabricmc:yarn:${property('deps.yarn')}:v2"
            if (stonecutter.current.version == "1.21") {
                mappings "dev.architectury:yarn-mappings-patch-neoforge:1.21+build.4"
            }
        }
    }

    if (modPlatform == "forge") {
        forge "net.minecraftforge:forge:${stonecutter.current.version}-${property('deps.forge')}"
        modCompileOnly "me.shedaniel.cloth:cloth-config-forge:${property('deps.cloth-config')}"
        compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
        implementation(include("io.github.llamalad7:mixinextras-forge:0.4.1"))
    } else if (modPlatform == "neoforge") {
        neoForge "net.neoforged:neoforge:${property('deps.neoforge')}"
        modCompileOnly "me.shedaniel.cloth:cloth-config-neoforge:${property('deps.cloth-config')}"
    }

    implementation("com.google.auto.service:auto-service-annotations:1.1.1")
    annotationProcessor("com.google.auto.service:auto-service:1.1.1")

    include stonecutter.node.sibling("defaults").project
}

/*
fletchingTable {
    defaultMixinEnvironment = "auto"
}
 */

loom {
    accessWidenerPath = project(":").file("src/main/resources/citresewn.accesswidener")

    if (modPlatform == "forge") {
        forge {
            convertAccessWideners.set(true)
            mixinConfigs("citresewn.mixins.json")
        }
    }

    runConfigs.client.ideConfigGenerated = false
    runConfigs.server.ideConfigGenerated = false
}

java {
    withSourcesJar()
    sourceCompatibility = isJava21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
    targetCompatibility = isJava21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

    archivesBaseName = property('mod.jarname')
}

processResources {
    inputs.property 'mod_version', version
    inputs.property 'mod_target_mc', project.property('mod.target-mc')

    if (modPlatform == "forge" || modPlatform == "neoforge") {
        if (modPlatform != "forge") {
            exclude "META-INF/mods.toml"
        }
        if (modPlatform != "neoforge") {
            exclude "META-INF/neoforge.mods.toml"
        }
        filesMatching(modPlatform == "forge" ? "META-INF/mods.toml" : "META-INF/neoforge.mods.toml") {
            expand 'mod_version': version, 'mod_target_mc': project.property('mod.target-mc')
        }
    }
}

jar {
    archiveClassifier.set 'dev'

    from rootProject.file('LICENSE')
}

remapJar {
    if (modPlatform == "neoforge") {
        atAccessWideners.add("citresewn.accesswidener")
    }
}

if (stonecutter.current.isActive) {
    rootProject.tasks.register('buildActive') {
        setGroup 'project'

        dependsOn tasks.getByName('build')
    }

    plugins.apply 'com.modrinth.minotaur'
    plugins.apply 'com.matthewprenger.cursegradle'


    modrinth {
        token = "TOKEN_MODRINTH"
        projectId = property('publish.modrinth.id')
        uploadFile = remapJar
        changelog = java.nio.file.Files.readString(rootProject.file("Changelog.md").toPath())
        versionNumber = version
        versionName = "v" + version
        versionType = 'beta'
        additionalFiles = [sourcesJar.outputs.getFiles().singleFile]
        gameVersions = Arrays.asList(property('publish.target-mc').split(","))
    }

    curseforge {
        apiKey = "TOKEN_CURSEFORGE"
        project {
            id = property('publish.curseforge.id')
            changelogType = 'markdown'
            changelog = rootProject.file('Changelog.md')
            releaseType = 'beta'
            mainArtifact(remapJar) {
                displayName = "v" + version
            }
            addGameVersion 'Fabric'
            for (String version : property('publish.target-mc').split(","))
                addGameVersion version
        }
        options {
            forgeGradleIntegration = false
        }
    }

    rootProject.tasks.register('publishActive') {
        setGroup 'project'

        if (gradle.startParameter.taskNames.contains(it.name)) {
            System.out.println("Type the task name to confirm (${it.name}): ")
            try (BufferedReader input = new BufferedReader(new InputStreamReader(System.in))) {
                if (input.readLine() != it.name)
                    throw new GradleException('Task cancelled by user')
            }
        }

        Task remapJarTask = tasks.getByName 'remapJar'
        Task curseforgeTask = tasks.getByName 'curseforge'
        Task modrinthTask = tasks.getByName 'modrinth'

        dependsOn remapJarTask, curseforgeTask, modrinthTask
        curseforgeTask.mustRunAfter remapJarTask
        modrinthTask.mustRunAfter remapJarTask
    }
}
